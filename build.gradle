plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

group 'io.github.lprakashv'
version '0.1.1'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

def hasCredentials = hasProperty('ossrhUser') && hasProperty('ossrhPassword')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'patternmatcher4j'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'patternmatcher4j'
                description = 'A Functional programming style pattern matcher library for Java'
                url = 'http://lprakashv.github.io/patternmatcher4j/'
                scm {
                    url 'https://github.com/lprakashv/patternmatcher4j'
                    connection 'scm:git:git://github.com/lprakashv/patternmatcher4j.git'
                    developerConnection 'scm:git:git@github.com:lprakashv/patternmatcher4j.git'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lprakashv'
                        name = 'Lalit Vatsal'
                        email = 'lprakashv@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if (hasCredentials) {
                credentials {
                    username ossrhUser
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}